PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>

SELECT DISTINCT (str(?class) as ?term)
WHERE {
  ?class a owl:Class .
  FILTER(!strstarts(str(?class),str(UBERON:)) )
  {
    ?class rdfs:subClassOf ?parentClass .
    FILTER( strstarts(str(?parentClass),str(UBERON:)) )
  }
  UNION
  {
    ?class  owl:equivalentClass/
            owl:intersectionOf/
            rdf:rest*/rdf:first ?equivalentClass.
    FILTER( strstarts(str(?equivalentClass),str(UBERON:)) )
  }
}