PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX MBA: <http://purl.obolibrary.org/obo/MBA_>
PREFIX DMBA: <http://purl.obolibrary.org/obo/DMBA_>

SELECT DISTINCT ?o ?s ?olabel ?slabel (str(?o_mba) as ?user_olabel) (str(?s_mba) as ?user_slabel)
WHERE{
    ?s_mba a owl:Class .
    FILTER (STRSTARTS(STR(?s_mba), str(MBA:)) || STRSTARTS(STR(?s_mba), str(DMBA:)))
    {
        ?s_mba rdfs:subClassOf ?s .
    }
    UNION
    {
        ?s_mba  owl:equivalentClass/
                owl:intersectionOf/
                rdf:rest*/rdf:first ?equivalentClassRestriction.
        ?equivalentClassRestriction owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000050> .
        ?equivalentClassRestriction owl:someValuesFrom ?s .
    }
    ?s_mba rdfs:subClassOf ?restriction.
    ?restriction owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000050> .
    ?restriction owl:someValuesFrom ?o_mba .
    ?o_mba rdfs:subClassOf ?o .
    ?s rdfs:label ?slabel .
    ?o rdfs:label ?olabel .

    FILTER (STRSTARTS(STR(?o_mba), str(MBA:)) || STRSTARTS(STR(?o_mba), str(DMBA:)))
    FILTER (isIRI(?o) && STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/UBERON_"))
    FILTER (isIRI(?s) && STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/UBERON_"))
    FILTER (?o != ?s)

}